include(CheckIncludeFile)
include(CheckSymbolExists)

function(configure_trn)
    #
    # Perform inspections of the system and configure accordingly.
    #
    check_include_file(pwd.h        I_PWD)
    check_include_file(sgtty.h      I_SGTTY)
    check_include_file(strings.h    I_STRINGS)
    check_include_file(sys/filio.h  I_SYS_FILIO)
    check_include_file(sys/ioctl.h  I_SYS_IOCTL)
    check_include_file(sys/stat.h   I_SYS_STAT)
    check_include_file(sys/utime.h  I_SYS_UTIME)
    check_include_file(sys/wait.h   I_SYS_WAIT)
    check_include_file(termcap.h    I_TERMCAP)
    check_include_file(termios.h    I_TERMIOS)
    check_include_file(unistd.h     I_UNISTD)
    check_include_file(utime.h      I_UTIME)
    check_include_file(vfork.h      I_VFORK)
    if(I_PWD)
        check_symbol_exists(getpwent "pwd.h" HAS_GETPWENT)
    endif()
    if(I_STRINGS)
        check_symbol_exists(strcasecmp "strings.h" HAS_STRCASECMP)
    endif()
    if(I_UNISTD)
        check_symbol_exists(getdomainname "unistd.h" HAS_GETDOMAINNAME)
        check_symbol_exists(gethostname "unistd.h" HAS_GETHOSTNAME)
    endif()
    if(NOT HAS_GETHOSTNAME)
        set(CMAKE_REQUIRED_LIBRARIES "ws2_32.lib")
        check_symbol_exists(gethostname "WinSock2.h" HAS_GETHOSTNAME)
        unset(CMAKE_REQUIRED_LIBRARIES)
    endif()
    set(HAS_TERMLIB ON)
    set(MBOX_CHAR "F")
    if(WIN32)
        set(MSDOS ON)
    endif()
    if(NOT WIN32)
        set(HAS_NEWS_ADMIN ON)
        set(NEWS_ADMIN "root")
    endif()
    set(NORMSIG ON)
    set(HOSTBITS ON)
    set(ORGNAME "%X/organization")
    set(PHOSTNAME "localhost")
    set(ROOT_ID "0")
    set(SERVER_NAME "news.gmane.io")
    set(SIGNAL_T "void")
    set(SUPPORT_XTHREAD ON)
    set(THREADDIR "%X/threads")
    set(TRN_INIT OFF)
    set(TRN_SELECT ON)
    if(WIN32)
        string(REPLACE "\\" "/" _ProgramData "$ENV{ProgramData}")
        set(NEWSLIB "${_ProgramData}/trn/news")
        set(PRIVLIB "${_ProgramData}/trn/lib")
    endif()
    set(EXTRAINEWS "")
    set(GROUPDESC "")
    set(NEWSSPOOL "%X/spool")

    configure_file("${CMAKE_CURRENT_FUNCTION_LIST_DIR}/config.h.in" include/config.h)
    set(STRING_CASE_COMPARE_SOURCES "")
    if(NOT HAS_STRCASECMP)
        set(STRING_CASE_COMPARE_SOURCES "string_case_compare.cpp")
    endif()
    set(STRING_CASE_COMPARE_H "${CMAKE_CURRENT_FUNCTION_LIST_DIR}/string_case_compare.${CMAKE_HOST_SYSTEM_NAME}.h.in")
    if(NOT EXISTS "${STRING_CASE_COMPARE_H}")
        message(FATAL_ERROR "Unknown system ${CMAKE_HOST_SYSTEM_NAME}; expected 'Linux' or 'Windows'")
    endif()
    configure_file("${STRING_CASE_COMPARE_H}" "include/string_case_compare.h")
    set(STRING_CASE_COMPARE_SOURCES "${STRING_CASE_COMPARE_SOURCES}" PARENT_SCOPE)
    set(STRING_CASE_COMPARE_H "${STRING_CASE_COMPARE_H}" PARENT_SCOPE)
endfunction()
